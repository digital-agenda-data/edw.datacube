PREFIX dc: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dad-prop: <http://semantic.digital-agenda-data.eu/def/property/>

SELECT DISTINCT ?uri ?notation ?definition ?label ?short_label ?note
                ?source_label ?source_definition ?source_notes ?source_url
                ?groupName ?parentOrder ?innerOrder
WHERE {

  { ?uri skos:notation ?notation }
  UNION { ?uri skos:prefLabel ?label }
  UNION { ?uri skos:altLabel ?short_label }
  UNION { ?uri skos:note ?note }
  UNION { ?uri skos:definition ?definition }
  {%- if dimension == 'indicator' %}
    UNION { ?uri dc:source [
               skos:prefLabel ?source_label ;
               skos:definition ?source_definition ;
               foaf:page ?source_url ] }
    UNION {?uri dc:source [skos:note ?source_notes] }
  {% endif %}
  UNION {
    ?uri dad-prop:membership ?parentNode.
    OPTIONAL {?parentNode dad-prop:order ?innerOrder} .
    ?parentNode dad-prop:member-of ?parentUri.
    ?parentUri skos:prefLabel ?groupName.
    OPTIONAL {?parentUri dad-prop:order ?parentOrder}.
  }

FILTER (
{%- for uri in uri_list %}
  ?uri = {{ uri|uri_n3 }}
  {%- if not loop.last %} || {% endif %}
{%- endfor %}
)

}
LIMIT 1000
