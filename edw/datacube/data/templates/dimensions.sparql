PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dad-prop: <http://semantic.digital-agenda-data.eu/def/property/>
SELECT DISTINCT ?dimension ?notation ?dimension_class ?label ?comment ?group_notation WHERE {
  {{ dataset|uri_n3 }} qb:structure ?structure .
  ?structure qb:component ?componentSpec .
  ?componentSpec ?type ?dimension .
  {?dimension a ?dimension_class FILTER (
    ?dimension_class = <http://purl.org/linked-data/cube#DimensionProperty> ||
    ?dimension_class = <http://purl.org/linked-data/cube#AttributeProperty> ||
    ?dimension_class = <http://purl.org/linked-data/cube#MeasureProperty> ||
    ?dimension_class = <http://semantic.digital-agenda-data.eu/def/class/DimensionGroupProperty>
  )}

  OPTIONAL {
    ?dimension rdfs:label ?label
  }
  OPTIONAL {
    ?dimension skos:notation ?notation .
  }
  OPTIONAL {
    ?dimension rdfs:comment ?comment .
  }
  OPTIONAL {
    ?componentSpec qb:order ?componentSpecOrder .
  }
  OPTIONAL {
    ?dimension dad-prop:grouped-using ?group .
    ?group skos:notation ?group_notation .
  }
}
ORDER BY ?componentSpecOrder
LIMIT 1000
